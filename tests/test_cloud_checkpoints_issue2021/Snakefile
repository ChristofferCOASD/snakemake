import os

# This test file is adapted from this one:
# https://github.com/snakemake/snakemake/blob/e03a3b42eea89d512290bf98ee7d77ce2e17447c/tests/test_cloud_checkpoints_issue574/Snakefile
from snakemake.remote.GS import RemoteProvider as GSRemoteProvider
GS = GSRemoteProvider()

rule all:
    input:
        "landsat-data.txt.bz2"

checkpoint copy:
    input:
        GS.remote("gcp-public-data-landsat/LC08/01/001/003/LC08_L1GT_001003_20170430_20170501_01_RT/LC08_L1GT_001003_20170430_20170501_01_RT_MTL.txt")
    output:
        directory("landsat-dir")
    resources:
        mem_mb=100
    run:
        shell('mkdir {output}')
        for i in range(3):
            shell('cp {input} {output}/landsat-data-$(date "+%s%N").txt')

def get_files(wc):
    checkpoint_output = checkpoints.copy.get().output[0]
    query = os.path.join(checkpoint_output, "landsat-data-{s}.txt")
    return expand(query, s=glob_wildcards(query).s)

rule pack:
    input: get_files
    output:
        "landsat-data.txt.bz2"
    conda:
        "env.yml"
    log:
        "logs/pack.log"
    shell:
        "cat {input} | bzip2 -c > {output}; echo successful > {log}"
